# Maintainer: Jacob Mischka <jacob@mischka.me>
# Comaintainer: Pedro A. LÃ³pez-Valencia <https://aur.archlinux.org/user/vorbote>
pkgname=brave
pkgver=0.8.3
pkgrel=1
pkgdesc='Web browser that blocks ads and trackers by default.'
arch=('x86_64')
url='https://www.brave.com/'
license=('custom')
depends=('gtk2' 'nss' 'alsa-lib' 'libnotify' 'gconf' 'libxtst' 'desktop-file-utils')
makedepends=('npm')
install="$pkgname".install
provides=('brave-browser')
source=("https://github.com/brave/browser-laptop/archive/v"$pkgver"dev.tar.gz"
        "MPL2::https://raw.githubusercontent.com/brave/browser-laptop/master/LICENSE.txt"
	'brave.desktop')
sha384sums=('f9bc24904ae66ec783a8200e99b87c4287a76079fa78db0dd57be1bf459d4c418aa549e55f1eb708c433b04ed2736ee4'
            'b27caa103555393992e6e1de1c2663f3ecf8339054e1aee8961406c8cbc9d677ba78b4bab6efe7210143818f9207d16b'
            'f950675fb4a3f9e48374f8a2667e7a45889206a3062c8182e474143607fc26bd17e852a1ef494607dbd3ff4de325e05f')

build() {
	cd "$srcdir"/browser-laptop-"$pkgver"dev
	npm install node-gyp@3.2.1
	npm install
	CHANNEL=dev npm run build-package
}

package() {
	cd "$srcdir"/browser-laptop-"$pkgver"dev

	install -dm0755 "$pkgdir"/usr/lib

	cp -a --reflink=auto Brave-linux-x64 "$pkgdir"/usr/lib/brave-browser

	install -Dm0644 "$srcdir"/brave.desktop "$pkgdir"/usr/share/applications/brave.desktop

	install -dm0755 "$pkgdir"/usr/bin

	ln -s /usr/lib/brave-browser/Brave "$pkgdir"/usr/bin/brave

	install -Dm0644 res/app.png "$pkgdir"/usr/share/pixmaps/brave.png

	install -dm0755 "$pkgdir"/usr/share/licenses/brave-browser

	cp --reflink=auto "$srcdir"/MPL2  "$pkgdir"/usr/share/licenses/brave-browser/MPL2

	mv "$pkgdir"/usr/lib/brave-browser/{LICENSE,LICENSES.chromium.html} "$pkgdir"/usr/share/licenses/brave-browser/
}
